// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A basic math library.
 */
class Math {
    
    static Array twoPowerI;

    /** Initializes the library. */
    function void init() {
        let twoPowerI = Array.new(16);
        let twoPowerI[0] = 1;
        let twoPowerI[1] = 2;
        let twoPowerI[2] = 4;
        let twoPowerI[3] = 8;
        let twoPowerI[4] = 16;
        let twoPowerI[5] = 32;
        let twoPowerI[6] = 64;
        let twoPowerI[7] = 128;
        let twoPowerI[8] = 256;
        let twoPowerI[9] = 512;
        let twoPowerI[10] = 1024;
        let twoPowerI[11] = 2048;
        let twoPowerI[12] = 4096;
        let twoPowerI[13] = 8192;
        let twoPowerI[14] = 16384;
        let twoPowerI[15] = 16384 + 16384;
        return;
    }

    /** Returns if jth bit of x is 1 */
    function boolean bit(int x, int j){
        return ~((x & twoPowerI[j]) = 0);
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if(x < 0){
            let x = -x;
        }
        return x;
    }

    /** Returns the product of x and y. */
    function int multiply(int x, int y) {
        var int sum, shiftedX,j;

        let sum = 0;
        let shiftedX = x;
        let j = 0;

        while(j < 16){
            if(Math.bit(y,j)){
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let j = j + 1;
        }

        return sum;
    }

    /** Returns the integer part of x/y. */
    function int divide(int x, int y) {
        var int q,result;
        var boolean pos;

        let pos = ((x < 0) = (y < 0));

        let x = Math.abs(x);
        let y = Math.abs(y);

        if(y > x){
            return 0;
        }

        let q = Math.divide(x,y + y);
        
        if((x - (2 * q * y)) < y){
            let result = q + q;
        }else{
            let result = q + q + 1;
        }

        if(pos){
            return result;
        }else{
            return -result;
        }

    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y,j,temp,tempQ;

        let y = 0;
        let j = 7;

        while(~(j < 0)){
            let temp = y + twoPowerI[j];
            let tempQ = temp * temp;
            //avoid overflow
            if(~(tempQ > x) & (tempQ > 0)){
                let y = temp;
            }
            let j = j - 1;
        }

        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if(a > b){
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if(a < b){
            return a;
        }
        return b;
    }

    /** helper function: mod/
    function int mod(int a, int b){
        return x - (Math.divid(a,b) * b);
    }

    /** helper function two to the*/
    function int twoPowerOfI(int i){
        return twoPowerI[i];
    }
}
